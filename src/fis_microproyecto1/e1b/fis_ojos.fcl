FUNCTION_BLOCK Ceguera

VAR_INPUT
    agudeza_visual : REAL; RANGE := (0.2 .. 10);
    refraccion : REAL; RANGE := (-10 .. -0.25);
    fatiga_ocular : REAL; RANGE := (0 .. 10);
    dolor_ocular : REAL; RANGE := (0 .. 10);
END_VAR

VAR_OUTPUT
    astigmatismo : REAL; RANGE := (0 .. 6);
    miopia : REAL; RANGE := (0 .. 9);
    hipermetropia : REAL; RANGE := (0 .. 8);
END_VAR

FUZZIFY agudeza_visual
    TERM muy_pobre := trape 0.2 0.2 0.5 2;
    TERM regular := trian 1 3 6;
    TERM pobre := trian 5 6.5 8;
    TERM buena := trape 7 9 10 10;
END_FUZZIFY

FUZZIFY refraccion
    TERM baja := trape -3 -1 -0.25 -0.25;
    TERM media := trian -5 -3 -2.5;
    TERM alta := trian -7 -5 -4.5;
    TERM muy_alta := trape -10 -10 -7 -6;
END_FUZZIFY

FUZZIFY fatiga_ocular
    TERM nula := trian 0 0 2;
    TERM leve := trian 1 2 4;
    TERM moderada := trian 3 4 6;
    TERM alta := trian 5 6 8;
    TERM intensa := trian 7 10 10;
END_FUZZIFY

FUZZIFY dolor_ocular
    TERM nulo := trian 0 0 2;
    TERM leve := trian 1.5 4 5;
    TERM moderado := trian 4.5 6 7;
    TERM intenso := trian 6.5 8 10;
END_FUZZIFY

DEFUZZIFY astigmatismo
    TERM nulo := trape 0 0 0 0.35;
    TERM leve := trian 0.25 0.725 1.2;
    TERM moderada := trian 0.75 2 3.5;
    TERM alta := trape 3 5 6 6;
    METHOD : COG;
    DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY miopia
    TERM nula := trape 0 0 0.5 1;
    TERM leve := trian 0.25 1.7 3;
    TERM moderada := trian 2.75 4 6;
    TERM alta := trape 5 8 9 9;
    METHOD : COG;
    DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY hipermetropia
    TERM nula := trape 0 0 0.7 1.5;
    TERM leve := trian 0.5 1.5 2.5;
    TERM moderada := trian 1.5 3 5;
    TERM alta := trape 4 7 8 8;
    METHOD : COG;
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    // Reglas para Astigmatismo
    RULE 1 : IF (agudeza_visual IS muy_pobre) AND (refraccion IS muy_alta) AND (fatiga_ocular IS alta OR fatiga_ocular IS intensa) 
            THEN astigmatismo IS alta WITH 1.0;        
    RULE 2 : IF (agudeza_visual IS pobre) AND (refraccion IS alta) AND (dolor_ocular IS moderado OR dolor_ocular IS intenso) 
            THEN astigmatismo IS moderada WITH 0.8;
    RULE 3 : IF (agudeza_visual IS regular) AND (refraccion IS media) AND (fatiga_ocular IS moderada) 
            THEN astigmatismo IS leve WITH 0.7;
    RULE 4 : IF (fatiga_ocular IS intensa) AND (dolor_ocular IS intenso) 
            THEN astigmatismo IS alta WITH 0.7;
    
    // Reglas para Miopía
    RULE 5 : IF (agudeza_visual IS muy_pobre) AND (refraccion IS muy_alta) AND (fatiga_ocular IS intensa) AND (dolor_ocular IS intenso) 
            THEN miopia IS alta WITH 1.0;
    RULE 6 : IF (agudeza_visual IS pobre) AND (refraccion IS alta) AND (fatiga_ocular IS moderada) 
            THEN miopia IS moderada WITH 0.8;
    RULE 7 : IF (agudeza_visual IS regular) AND (refraccion IS media) AND (fatiga_ocular IS leve) 
            THEN miopia IS leve WITH 0.7;
    RULE 8 : IF (agudeza_visual IS buena) AND (refraccion IS baja) AND (fatiga_ocular IS nula) AND (dolor_ocular IS nulo) 
            THEN miopia IS nula WITH 1.0;

    // Reglas para Hipermetropía
    RULE 9 : IF (agudeza_visual IS muy_pobre) AND (refraccion IS muy_alta) AND (fatiga_ocular IS intensa) AND (dolor_ocular IS intenso) 
            THEN hipermetropia IS alta WITH 0.9;
    RULE 10 : IF (agudeza_visual IS pobre) AND (refraccion IS alta) AND (fatiga_ocular IS alta) 
            THEN hipermetropia IS moderada WITH 0.8;
    RULE 11 : IF (agudeza_visual IS regular) AND (refraccion IS media) AND (fatiga_ocular IS moderada) 
            THEN hipermetropia IS leve WITH 0.7;
    RULE 12 : IF (agudeza_visual IS buena) AND (refraccion IS baja) AND (fatiga_ocular IS nula) AND (dolor_ocular IS nulo) 
            THEN hipermetropia IS nula WITH 1.0;

    // Reglas para condiciones combinadas
    RULE 13 : IF (agudeza_visual IS muy_pobre) AND (refraccion IS muy_alta) AND (fatiga_ocular IS intensa) 
            THEN astigmatismo IS alta, miopia IS alta WITH 0.9;
    RULE 14 : IF (agudeza_visual IS pobre) AND (refraccion IS alta) AND (dolor_ocular IS intenso) 
            THEN astigmatismo IS moderada, hipermetropia IS moderada WITH 0.8;
 
    // Reglas de exclusión
    RULE 15 : IF (agudeza_visual IS buena) AND (fatiga_ocular IS nula) AND (dolor_ocular IS nulo) 
            THEN astigmatismo IS nulo, miopia IS nula, hipermetropia IS nula WITH 1.0;
    RULE 16 : IF (refraccion IS baja) AND (agudeza_visual IS buena) 
            THEN astigmatismo IS nulo, miopia IS nula WITH 0.9;

END_RULEBLOCK


END_FUNCTION_BLOCK
